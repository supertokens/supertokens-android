apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
def publishVersionID = "0.3.5"

android {
    compileSdkVersion 32
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 32
        versionCode 1
        versionName publishVersionID
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    groupId = 'com.supertokens'
                    artifactId = 'session'
                    version = publishVersionID

                    from components.release
                }
            }
        }
    }

    libraryVariants.all { variant ->

        task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
            title = "Documentation for Android $android.defaultConfig.versionName b$android.defaultConfig.versionCode"
            destinationDir = new File("${project.getProjectDir()}/../docs")
            source = variant.javaCompile.source

            ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
            doFirst { classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar) }

            description "Generates Javadoc for $variant.name."

            options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PUBLIC
            options.links("http://docs.oracle.com/javase/7/docs/api/");
            options.links("http://developer.android.com/reference/");
            exclude '**/BuildConfig.java'
            exclude '**/R.java'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    compileOnly "com.squareup.okhttp3:okhttp:4.1.0"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'com.google.code.gson:gson:2.8.5'
    testImplementation 'com.squareup.okhttp3:okhttp:4.1.0'
    testImplementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    testImplementation 'com.squareup.retrofit2:retrofit:2.3.0'
    testImplementation 'com.squareup.retrofit2:converter-gson:2.3.0'
}
